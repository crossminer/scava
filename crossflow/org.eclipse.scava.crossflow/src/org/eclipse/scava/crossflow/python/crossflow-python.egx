rule Workflow2Class 
	transform w : Workflow {
	
	parameters : Map{"w" = w,"package" = w.getPackageName("Python")}
	
	template : "workflow2class.egl"
	
	target : w.getPath() + w.name + ".py"
}

rule Type2Class 
	transform t : Type {
	
	guard : t.impl.isUndefined() //and s.csvFile.isUndefined()
	
	template : "type2class.egl"
	
	target : t.getPath() + t.name + ".py"
}

rule CsvSource2Source
	transform t : CsvSource {

	parameters : Map{"w" = t.eContainer()}

	template : "csvsource2source.egl"
	
	target : t.getImplPath() + t.name + ".py"	
	
	overwrite : false
}

rule CsvSink2Sink
	transform t : CsvSink {

	parameters : Map{"w" = t.eContainer()}

	template : "csvsink2sink.egl"
	
	target : t.getImplPath() + t.name + ".py"	
	
	overwrite : false
}

@greedy
rule Task2BaseClass 
	transform t : Task {

	guard : t.language.isDefined() and t.language.name=="Python"	
	
	parameters : Map{"w" = t.eContainer()}
	
	template : "task2baseclass.egl"
	
	target : t.getPath() + t.name + "Base.py"	
}

@greedy
rule Task2ImplClass 
	transform t : Task {
		
	guard : t.language.isDefined() and t.language.name=="Python"		
		
	parameters : Map{"w" = t.eContainer(),"package" = t.eContainer().getPackageName("Python")}
	
	template : "task2implclass.egl"
	
	target : t.getImplPath() + t.name + ".py"
	
	overwrite : false
}

@greedy
rule Steam2Class 
	transform s : Stream {
	
	parameters : Map{"w" = s.eContainer()}
	
	template : "stream2class.egl"
	
	target : s.getPath() + s.name + ".py"
}

@greedy
rule Steam2Consumer 
	transform s : Stream {
	
	parameters : Map{"t" = Task}
	
	template : "stream2consumer.egl"
	
	target : s.getPath() + s.name + "Consumer.py"
}

@cached
operation Any getPath() {
	var workflow = self;
	if (not self.isTypeOf(Workflow)) workflow = self.eContainer();
	
	if(not workflow.appliesTo("Python")) return null;	
	
	var lang = workflow.languages.select(l|l.name=="Python");
	
	var path = lang.first.genOutputFolder + "/" + workflow.getPackageName("Python").toPath();

	return path;
}

@cached
operation Any getImplPath() {
	var workflow = self;
	if (not self.isTypeOf(Workflow)) workflow = self.eContainer();
	
	if(not workflow.appliesTo("Python")) return null;
	
	var lang = workflow.languages.select(l|l.name=="Python");

	var path = lang.first.outputFolder + "/" + workflow.getPackageName("Python").toPath();

	return path;
}

@cached
operation Any getWorkflow() : Workflow {
	if (self.isTypeOf(Workflow)) return self;
	return self.eContainer();
}

operation String toPath() : String {
	return self.replaceAll("\\.", "/") + "/";
}

operation Workflow appliesTo(lang) {
	return not self.languages.select(l|l.name==lang).isEmpty();
}

operation Workflow getPackageName(lang : String) {
	var langs = self.languages.select(l | l.name == lang);
	var langExists = not langs.isEmpty;

	var package = self.name.ftlc;
	
	if(langExists) package = langs.first.package;
	
	return package;
}